plugins{
    id 'java'
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: 'architectury-plugin'
}

architectury{
    minecraft = project.minecraft_version
}

apply plugin: 'com.matthewprenger.cursegradle'

task buildAll{
    dependsOn(":${project.module_name}:fabric:build")
    dependsOn(":${project.module_name}:forge:build")
}

task copyArtifacts{
    dependsOn("buildAll")
    doLast {
        copy{
            from "${project.projectDir}/fabric/build/libs/${project.archives_base_name}-for-MC${project.minecraft_version}-${project.mod_version}-fabric.jar"
            into "${rootProject.projectDir}/artifacts"
        }

        copy{
            from "${project.projectDir}/forge/build/libs/${project.archives_base_name}-for-MC${project.minecraft_version}-${project.mod_version}-forge.jar"
            into "${rootProject.projectDir}/artifacts"
        }//*/
    }
}

if(keysExists) {
    task publishMod {
        finalizedBy(":${project.module_name}:fabric:modrinth")
        finalizedBy(":${project.module_name}:forge:modrinth")
        finalizedBy(":${project.module_name}:publishToCF")
    }

    curseforge {
        apiKey = project.keys.curseforge_key

        project {
            id = '397809' //Fabric version
            changelogType = "markdown"
            //changelog = '[See on Github](https://github.com/KosmX/emotes/commits/master)'
            changelog = changes
            releaseType = project.cfType
            addGameVersion "1.19"
            addGameVersion "1.19.1"
            addGameVersion "1.19.2"
            addGameVersion "Fabric"
            addGameVersion "Quilt"


            relations {
                requiredDependency 'fabric-api'
                optionalDependency 'modmenu'
                embeddedLibrary 'playeranimator'
                embeddedLibrary 'bendy-lib'
            }

            options {
                forgeGradleIntegration = false // FABRIC MOD
                javaVersionAutoDetect = false // defaults to true
            }

            mainArtifact("${project.getProjectDir()}/fabric/build/libs/${project.archives_base_name}-for-MC${project.minecraft_version}-${project.mod_version}-fabric.jar") {
                //displayName = archivesBaseName
            }
        }

        project {
            id = '403422' //Fabric version
            changelogType = "markdown"
            //changelog = '[See on Github](https://github.com/KosmX/emotes/commits/master)'
            changelog = changes
            releaseType = project.cfType
            addGameVersion "1.19"
            addGameVersion "1.19.1"
            addGameVersion "1.19.2"
            addGameVersion "Forge"


            relations {
                requiredDependency 'playeranimator'
                optionalDependency 'bendy-lib'
            }

            options {
                forgeGradleIntegration = false // ARCHITECTURY MOD
                javaVersionAutoDetect = false // defaults to true
            }

            mainArtifact("${project.getProjectDir()}/forge/build/libs/${project.archives_base_name}-for-MC${project.minecraft_version}-${project.mod_version}-forge.jar") {
                //displayName = archivesBaseName
            }
        }//*/

    }

    task publishToCF{
        dependsOn('buildAll')
        finalizedBy(tasks.curseforge)
    }


    //CurseGradle... That plugin should be updated

}
