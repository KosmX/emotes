plugins {
    id 'java-library'
    id 'maven-publish'
}

version project.mod_version

repositories {
    mavenCentral()
}

configurations {
    compileModule
}

dependencies {
    implementation project(':emotecraftCommon')
    implementation project(':executor')
    compileModule project(':emotecraftCommon')
    compileModule project(':executor')

    compileOnly "com.google.code.gson:gson:2.8.6"//That package is compiled into MC
}

tasks.withType(Copy){
    eachFile{
        rename{
            String filename->filename = filename.toLowerCase()
        }
        //Rename every file to lowercase. This is essential for the translations to work
        //Possibly creates other problems on other operating systems
    }
}

jar{
    from {
        configurations.compileModule.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

java{
    withJavadocJar()
    withSourcesJar()
}

publishing{
    publications{
        maven(MavenPublication) {
            groupId = 'com.kosmx.emotes'
            artifactId = 'emotesMain'
            version = project.version

            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

/* This testing stuff could be useful later...
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

 */