plugins {
    id 'java'
    id 'maven-publish'
    id 'signing' //why not if I can?
}

group project.maven_group
version project.mod_version

//sourceCompatibility = JavaVersion.VERSION_1_8
//targetCompatibility = JavaVersion.VERSION_1_8


configurations {
    dev
}

dependencies {
    implementation "dev.kosmx.player-anim:anim-core:${project.player_anim_core}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}
test {
    useJUnitPlatform()
}

compileJava {
    options.release.set 8 //Build on JDK 1.8
}

artifacts {
    dev(jar)
}

//-------- publishing --------

java{
    withSourcesJar()
    withJavadocJar()
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''

File secretPropsFile = project.rootProject.file('keys.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}
signing {
    if (secretPropsFile.exists()) {
        sign publishing.publications
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven

            artifactId = 'emotesAPI'

            artifact(jar)
            artifact(sourcesJar)
            artifact(javadocJar)


            pom.withXml {
                def depsNode = asNode().appendNode("dependencies")

                def animatorNode = depsNode.appendNode("dependency")
                animatorNode.appendNode("groupId", "dev.kosmx.player-anim")
                animatorNode.appendNode("artifactId", "anim-core")
                animatorNode.appendNode("version", project.player_anim_core)
                animatorNode.appendNode("scope", "compile")

            }

            pom{
                name = "emotesApi"
                description = "Minecraft Emotecraft api"
                url = 'https://github.com/KosmX/emotes'
                developers {
                    developer {
                        id = 'kosmx'
                        name = 'KosmX'
                        email = 'kosmx.mc@gmail.com'
                    }
                }

                licenses{
                    license{
                        name = "CC-BY-4.0 License"
                        url = "https://creativecommons.org/licenses/by/4.0/legalcode"
                    }
                }

                scm {
                    connection = 'scm:git:github.com/kosmx/emotes.git'
                    developerConnection = 'scm:git:github.com/kosmx/emotes.git'
                    url = 'https://github.com/KosmX/emotes'
                }
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        if (project.keysExists) {
            repositories {
                maven {
                    url = 'https://maven.kosmx.dev/'
                    credentials {
                        username = 'kosmx'
                        password = project.keys.kosmx_maven
                    }
                }
            }
        }
        else {
            mavenLocal()
        }
    }
}