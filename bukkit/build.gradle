plugins {
    id 'java'
    //id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'maven-publish'
}


project.archivesBaseName = project.archives_base_name
//project.version = project.mod_version
version = project.mod_version


repositories {
    mavenCentral()

    maven {
        name = "BucketMaven"
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        name = "dmulloy2"
        url "https://repo.dmulloy2.net/repository/public/"
    }
}

configurations {

    compileModule

    compileClasspath.extendsFrom compileModule
    runtimeClasspath.extendsFrom compileModule

}

dependencies {
    //testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    //testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation("org.spigotmc:spigot-api:${project.spigot_api}")
    implementation group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0"

    compileModule(project(':emotesAPI')){ transitive = false }
    compileModule(project(':executor')){ transitive = false }
    compileModule(project(':emotesServer')){ transitive = false }

    // Only for built-in resources, code is not used from here.
    compileModule(project(':emotesAssets')){ transitive = false }

    compileModule("dev.kosmx.player-anim:anim-core:${project.player_anim_core}"){ transitive = false }
}

processResources{

    inputs.property "version", project.version
    inputs.property "description", rootProject.mod_description

    filesMatching("plugin.yml"){
        expand version: project.version, description: rootProject.mod_description
    }

}

//shadowJar{
//    configurations = [project.configurations.compileModule]
//   archiveClassifier.set("bukkit")
//}

compileJava {
    dependsOn(':emotesAssets:publicationJar')
}

jar{
    from{
        configurations.compileModule.collect() {it.isDirectory() ? it : zipTree(it)}
    }
    archiveClassifier.set("bukkit")
}

task copyArtifacts{
    dependsOn('build')
    doLast {
        copy{
            from "${project.layout.buildDirectory}/libs/${project.archives_base_name}-${rootProject.mod_version}-bukkit.jar"
            into "${rootProject.projectDir}/artifacts"
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven

            artifactId = 'emotesBukkit'

            artifact(jar) {
                classifier ''
            }
            artifact(sourcesJar)


            pom{
                name = "emotesBukkit"
                description = "Minecraft Emotecraft Bukkit plugin"
                url = 'https://github.com/KosmX/emotes'
                developers {
                    developer {
                        id = 'kosmx'
                        name = 'KosmX'
                        email = 'kosmx.mc@gmail.com'
                    }
                }

                licenses{
                    license{
                        name = "CC-BY-4.0 License"
                        url = "https://creativecommons.org/licenses/by/4.0/legalcode"
                    }
                }

                scm {
                    connection = 'scm:git:github.com/kosmx/emotes.git'
                    developerConnection = 'scm:git:github.com/kosmx/emotes.git'
                    url = 'https://github.com/KosmX/emotes'
                }
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        if (project.keysExists) {
            repositories {
                maven {
                    url = 'https://maven.kosmx.dev/'
                    credentials {
                        username = 'kosmx'
                        password = project.keys.kosmx_maven
                    }
                }
            }
        }
        else {
            mavenLocal()
        }
    }
}
