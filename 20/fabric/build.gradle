
plugins {
    id 'maven-publish'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.modrinth.minotaur'

architectury{
    platformSetupLoomIde()
    fabric()
    minecraft = project.minecraft_version
}

loom{
    accessWidenerPath = project(":${project.module_name}:archCommon").loom.accessWidenerPath
    silentMojangMappingsLicense()
}



archivesBaseName = "${project.archives_base_name}-for-MC${project.minecraft_version}"
version = project.mod_version
group = project.maven_group


configurations {
    compileModule
    common
    commonModule

    common.extendsFrom commonModule
    compileModule.extendsFrom commonModule

    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    commonModule(project(':executor')) {transitive false}
    commonModule(project(':emotesAPI')) {transitive false}
    commonModule(project(':emotesServer')) {transitive false}
    commonModule(project(':emotesMain')) {transitive false}

    common(project(path: ":${project.module_name}:archCommon", configuration: 'namedElements')) {transitive false}

    compileModule(project(path: ":${project.module_name}:archCommon", configuration: "transformProductionFabric")) { transitive false }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}"){
        exclude group: "net.fabricmc.fabric-api"
    }
    //include "com.terraformersmc:modmenu:${project.modmenu_version}"


    include modImplementation("io.github.kosmx.bendy-lib:bendy-lib-fabric:${project.bendylib_version}")
    include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim}")
}


processResources {
    inputs.property "version", project.version
    inputs.property "description", rootProject.mod_description

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "description": rootProject.mod_description
    }
}


// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.

shadowJar{
    configurations = [project.configurations.compileModule]
   archiveClassifier.set("fabric-dev")
}

remapJar{
    injectAccessWidener = true
    dependsOn shadowJar
    input.set shadowJar.archiveFile
   archiveClassifier.set("fabric")
}

jar {
    //from {
    //    configurations.compileModule.collect { it.isDirectory() ? it : zipTree(it) }
    //}
    from "LICENSE"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}


// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(jar) {
                builtBy remapJar
            }
            //artifact("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar"){
            //	builtBy remapJar
            //}
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        mavenLocal()
    }
}


import com.modrinth.minotaur.dependencies.ModDependency

if(keysExists) {
    modrinth {
        versionType = project.cfType

        uploadFile = remapJar

        token = project.keys.modrinth_token
        // Get the GitHub Access Token you got from the basics part of this tutorial.
        projectId = "pZ2wrerK" // Enter your modrinth mod ID here.
        //System.out.println("Enter the version number:");
        versionNumber = "${project.mod_version}+${project.minecraft_version}-fabric"
        versionName = "${project.mod_version}"

        gameVersions = ["1.20.4"]
        changelog = changes
        loaders = ["fabric"]
        failSilently = false

        dependencies {
            required.project "fabric-api"
            optional.project "modmenu"
            embedded.project "bendy-lib"
            embedded.project "playeranimator"
        }
    }
}
